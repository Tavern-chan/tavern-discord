
plugins {
    id 'java'
    id 'application'
    id 'distribution'
    id 'com.tavern.groovy-common-conventions'
}

def appName = 'tavern'
def mainClassName = 'com.tavern.app.App'
version = '1.0.0'
description = 'Tavern discord integration'

dependencies {
    implementation project(':utilities')
    implementation project(':discord')
    implementation project(':domain')
    implementation project(':audio')
    implementation project(':repository')
    implementation project(':drinks')

    implementation libs.bundles.core
}

application {
    // Define the main class for the application.
    mainClass = mainClassName
}

distributions {
    main {
        distributionBaseName = 'tavern'

        contents {
            from('../', {
                include 'tavern-discord.properties'
            })

            include 'tavern*'
        }
    }
}

jar {
    duplicatesStrategy DuplicatesStrategy.INCLUDE
    manifest {
        attributes 'Main-Class': application.mainClass
    }
    archiveBaseName = 'tavern'

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

tasks.named('startScripts', CreateStartScripts) {
    applicationName = appName
}
